import com.github.sherter.googlejavaformatgradleplugin.GoogleJavaFormat
import com.github.sherter.googlejavaformatgradleplugin.VerifyGoogleJavaFormat

buildscript {
    repositories {
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.5.2"
        classpath('com.android.tools.build:gradle:3.1.0')
        classpath 'net.researchgate:gradle-release:2.8.1'
    }
}

plugins {
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

repositories {
    jcenter()
}

apply plugin: 'net.researchgate.release'

static def isAndroidProject(Project p) {
    return p.plugins.findPlugin('com.android.library')
}

ext {
    minSdkVersion = 14
    compileSdkVersion = 25
    targetSdkVersion = compileSdkVersion

    versions = [
            'autoValue'        : '1.5.3',
            'assertjcore'      : '3.8.0',
            'awaitility'       : '3.0.0',
            'guava'            : '27.0.1-android',
            'junit'            : '4.12',
            'logback'          : '1.2.3',
            'rxJava'           : '1.3.8',
            'rxJava2'          : '2.2.6',
            'slf4j'            : '1.7.25',
            'jsr305'           : '3.0.1',
            'hamcrestLibrary'  : '1.3',
            'mockito'          : '1.10.19',
            'androidLifecycle' : '1.1.1'
    ]
}

subprojects {
    ext.VERSION_NAME = properties.version

    repositories {
        jcenter()
    }

    group = GROUP
    version = VERSION_NAME

    afterEvaluate { proj ->
        if (isAndroidProject(proj)) {
            // for android, auto-format, even though that leads to worse error messages for syntax
            // errors
            android.libraryVariants.all { variant ->
                variant.javaCompiler.dependsOn(rootProject.tasks.format)
            }
        } else if (proj.plugins.findPlugin('java-library')) {
            proj.apply from: rootProject.file('gradle/binary_compatibility.gradle')
            // for Java (which is easier than android because AGP), ensure compilation is run before
            // formatting, since the compiler has much better error messages for syntax errors.
            rootProject.tasks.format.dependsOn(proj.tasks.compileTestJava)
        }
        // ensure that builds fail if code is not formatted properly
        proj.tasks.check.dependsOn(rootProject.tasks.verifyFormat)
    }
}

task format(type: GoogleJavaFormat) {
    exclude '**/package-info.java'
}
task verifyFormat(type: VerifyGoogleJavaFormat) {
    exclude '**/package-info.java'
}
task build(dependsOn: getTasksByName('build', true))
task uploadArchivesAggregate(dependsOn: getTasksByName('uploadArchives', true))

release {
    failOnUnversionedFiles = false
    tagTemplate = 'v$version'

    git {
        requireBranch = 'master'
        pushToRemote = 'upstream'
    }
}

afterReleaseBuild.dependsOn uploadArchivesAggregate

allprojects {
    apply plugin: 'com.github.hierynomus.license'

    license {
        header rootProject.file('apache2.header')
        exclude("**/AutoValue_*.java")
        mapping("java", "SLASHSTAR_STYLE")
    }

    getTasksByName('check', false).each { checkTask -> checkTask.dependsOn(tasks.withType(Javadoc)) }
}

nexusStaging {
    packageGroup = "com.spotify"
}
