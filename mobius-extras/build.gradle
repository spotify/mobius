apply plugin: 'java-library'
apply plugin: 'jacoco'

configurations {
    apt {
        visible false
    }
}

dependencies {
    api project(':mobius-core')
    compileOnly "com.google.auto.value:auto-value:${versions.autoValue}"
    implementation "org.slf4j:slf4j-api:${versions.slf4j}"
    implementation "com.google.code.findbugs:jsr305:${versions.jsr305}"

    apt "com.google.auto.value:auto-value:${versions.autoValue}"

    testImplementation project(':mobius-test')
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "com.google.guava:guava:${versions.guava}"
    testImplementation "org.awaitility:awaitility:${versions.awaitility}"
    testImplementation "com.google.auto.value:auto-value:${versions.autoValue}"
    testImplementation "org.hamcrest:hamcrest-library:${versions.hamcrestLibrary}"
}

def generatedSrcDir = new File(project.buildDir, "/generated/java")
def generatedTestSrcDir = new File(project.buildDir, "/generated-test/java")

sourceSets {
    generated.java.srcDir(generatedSrcDir)
    test.java.srcDirs += [ generatedTestSrcDir ]
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    options.annotationProcessorPath = configurations.apt

    options.compilerArgs << "-s"
    options.compilerArgs << "$generatedSrcDir"

    doFirst {
        // make sure that directory exists and is empty - the latter in order to avoid "endPosTable already set"
        file(generatedSrcDir).deleteDir()
        file(generatedSrcDir).mkdirs()
    }
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    options.annotationProcessorPath = configurations.apt

    options.compilerArgs << "-s"
    options.compilerArgs << "$generatedTestSrcDir"

    doFirst {
        // make sure that directory exists and is empty - the latter in order to avoid "endPosTable already set"
        file(generatedTestSrcDir).deleteDir()
        file(generatedTestSrcDir).mkdirs()
    }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
